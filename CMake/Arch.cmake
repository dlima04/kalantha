# Note: we don't actually support all of these architectures.
# it may be useful to detect them though.
macro(kta_enumerate_arch)
  set(KTA_IS_X86 OFF)
  set(KTA_IS_X86_64 OFF)
  set(KTA_IS_ARM OFF)
  set(KTA_IS_ARM64 OFF)
  set(KTA_IS_RISCV32 OFF)
  set(KTA_IS_RISCV64 OFF)
  set(KTA_IS_PPC OFF)
  set(KTA_IS_PPC64 OFF)
  set(KTA_IS_MIPS OFF)

  set(KTA_ARCH "${CMAKE_SYSTEM_PROCESSOR}")
  string(TOLOWER "${KTA_ARCH}" KTA_ARCH_LC)

  if(CMAKE_SIZEOF_VOID_P EQUAL 8 AND KTA_ARCH_LC MATCHES "^(x86_64|amd64)$")
    message(STATUS "Detected target architecture: x86_64")
    set(KTA_IS_X86_64 ON)
  elseif(CMAKE_SIZEOF_VOID_P EQUAL 4 AND KTA_ARCH_LC MATCHES "^(x86|i[3-6]86)$")
    message(STATUS "Detected target architecture: x86")
    set(KTA_IS_X86 ON)
  elseif(KTA_ARCH_LC MATCHES "^(aarch64|arm64)$")
    message(STATUS "Detected target architecture: arm64")
    set(KTA_IS_ARM64 ON)
  elseif(KTA_ARCH_LC MATCHES "^(arm|armv7|armv6l)$")
    message(STATUS "Detected target architecture: arm")
    set(KTA_IS_ARM ON)
  elseif(KTA_ARCH_LC MATCHES "^(riscv64)$")
    message(STATUS "Detected target architecture: riscv64")
    set(KTA_IS_RISCV64 ON)
  elseif(KTA_ARCH_LC MATCHES "^(riscv32)$")
    message(STATUS "Detected target architecture: riscv32")
    set(KTA_IS_RISCV32 ON)
  elseif(KTA_ARCH_LC MATCHES "^(ppc64|powerpc64)$")
    message(STATUS "Detected target architecture: ppc64")
    set(KTA_IS_PPC64 ON)
  elseif(KTA_ARCH_LC MATCHES "^(ppc|powerpc)$")
    message(STATUS "Detected target architecture: ppc")
    set(KTA_IS_PPC ON)
  elseif(KTA_ARCH_LC MATCHES "^(mips|mipsel|mips64)$")
    message(STATUS "Detected target architecture: mips")
    set(KTA_IS_MIPS ON)
  else()
    message(WARNING "Unrecognized architecture: ${KTA_ARCH}")
  endif()
endmacro()
